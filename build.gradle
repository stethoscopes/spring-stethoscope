plugins {
    id 'java'
    id 'war'
    id 'org.springframework.boot' version '3.2.0'
    id 'io.spring.dependency-management' version '1.1.4'
}

group = 'org.dev'
version = '0.0.1'

java {
    sourceCompatibility = '17'
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

jar {
    enabled = false
}

repositories {
    mavenCentral()
}

dependencies {
    // Spring
    implementation 'org.springframework.boot:spring-boot-starter-web'
//    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-security'

    // DB
    implementation 'org.mariadb.jdbc:mariadb-java-client'
    implementation 'org.apache.commons:commons-dbcp2:2.9.0'

    // Lombok
    compileOnly 'org.projectlombok:lombok'
    annotationProcessor 'org.projectlombok:lombok'
    // lombok getter setter mapping
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'

    // Jasypt
    implementation 'org.jasypt:jasypt:1.9.3'
    implementation 'org.jasypt:jasypt-spring31:1.9.3'

    // Sentry
    implementation 'io.sentry:sentry-spring:5.5.0'
    implementation 'io.sentry:sentry-logback:5.5.0'

    // SWAGGER
    implementation 'org.springdoc:springdoc-openapi-starter-webmvc-ui:2.1.0'
}

tasks.register('createProperties') {
    dependsOn 'processResources'

    doLast {
        def versionPropertiesFile = layout.buildDirectory.file("resources/main/properties/version.properties").get().asFile
        versionPropertiesFile.parentFile.mkdirs()

        def versionProperties = new Properties()
        versionProperties['project.version'] = project.version.toString()

        versionPropertiesFile.withWriter { writer ->
            versionProperties.store(writer, null)
        }
    }
}

tasks.named('test') {
    useJUnitPlatform()
}

tasks.named("bootJar") {
    layered {
        enabled = true
    }
}